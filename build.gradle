/*ยง
  ===========================================================================
  GraphsJ
  ===========================================================================
  Copyright (C) 2009-2016 Gianluca Costa
  ===========================================================================
  Licensed under the Apache License, Version 2.0 (the "License");
  you may not use this file except in compliance with the License.
  You may obtain a copy of the License at

       http://www.apache.org/licenses/LICENSE-2.0

  Unless required by applicable law or agreed to in writing, software
  distributed under the License is distributed on an "AS IS" BASIS,
  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
  See the License for the specific language governing permissions and
  limitations under the License.
  ===========================================================================
*/

buildscript {
    repositories {
        maven {
            url 'https://dl.bintray.com/giancosta86/Hephaestus'
        }

        jcenter()
    }

    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.1'
        classpath 'info.gianlucacosta.aurora:aurora:2.1'
        classpath 'info.gianlucacosta.moonlicense:moonlicense-gradle:4.0'
    }
}

import org.apache.tools.ant.filters.ReplaceTokens


apply plugin: 'scala'
apply plugin: 'info.gianlucacosta.aurora'
apply plugin: 'application'


group = 'info.gianlucacosta.graphsj'
archivesBaseName = 'graphsj'
version = '4.1'

description = 'ScalaFX didactic application for graph algorithms'

project.ext.facebookPage = 'https://www.facebook.com/graphsj'


mainClassName = "info.gianlucacosta.graphsj.App"


dependencies {
    compile 'info.gianlucacosta.graphsj:graphsj-sdk:4.0'

    compile 'org.scala-lang:scala-xml:2.11.0-M4'
    compile 'com.thoughtworks.xstream:xstream:1.4.8'
    compile 'org.reflections:reflections:0.9.10'
    compile 'javax.json:javax.json-api:1.0'
    compile 'org.glassfish:javax.json:1.0.4'
}


processResources {
    filesMatching('**/App.properties.xml') {
        filter ReplaceTokens, tokens: [
                'name'          : project.name,
                'version'       : project.version,
                'copyrightYears': project.moonLicense.getCopyrightYears(),
                'license'       : project.moonLicense.license.name,
                'url'           : project.url.toString(),
                'facebookPage'  : project.facebookPage,
                'release'       : project.auroraSettings.release.toString()
        ]
    }
}



startScripts {
    def originalWindowsTemplateString = windowsStartScriptGenerator.template.asString()
    def javawTemplateString =
            originalWindowsTemplateString
                    .replace("java.exe", "javaw.exe")
                    .replace("\"%JAVA_EXE%\" %DEFAULT_JVM_OPTS%", "start \"\" /B \"%JAVA_EXE%\" %DEFAULT_JVM_OPTS%")
    def javawTemplate = project.resources.text.fromString(javawTemplateString)
    windowsStartScriptGenerator.template = javawTemplate
}


aurora {
    docTask = 'scaladoc'
    gitHubUser = 'giancosta86'

    author {
        name = 'Gianluca Costa'
        email = 'gianluca@gianlucacosta.info'
        url = 'http://gianlucacosta.info/'
    }

    bintray {
        user = project.bintrayUser
        key = project.bintrayKey
        repo = 'Hephaestus'
        licenses = ['Apache-2.0']
        labels = ['GraphsJ', 'graphs', 'didactic', 'application', 'Java', 'Scala', 'JavaFX', 'ScalaFX']
    }
}


moonLicense {
    license = apache2

    productInfo {
        productName = 'GraphsJ'
        inceptionYear = 2009
        copyrightHolder = 'Gianluca Costa'
    }
}
